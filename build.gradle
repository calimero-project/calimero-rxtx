plugins {
  id "com.github.ben-manes.versions" version "0.44.0"
  id 'maven-publish'
  id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

apply plugin:'java-library'

group = 'com.github.calimero'
version = '2.6-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'


compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Jar) {
	from("${projectDir}") {
        include 'LICENSE'
        into "META-INF"
    }
    if (name.equals('sourcesJar')) {
    	from("${projectDir}") {
		    include 'README.md', 'build.gradle', 'settings.gradle', 'gradle*/**'
        }
    }
}

dependencies {
    api "com.github.calimero:calimero-core:${version}"
    api 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.neuronrobotics:nrjavaserial:5.2.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero RXTX Adapter'
                description = 'Adapter to use the RXTX library for serial communication'
                url = 'https://github.com/calimero-project/calimero-rxtx'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:calimero-project/calimero-rxtx.git'
                    url = 'https://github.com/calimero-project/calimero-rxtx.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
