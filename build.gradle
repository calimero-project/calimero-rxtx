plugins {
  id "com.github.ben-manes.versions" version "0.46.0"
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

group = 'io.calimero'
version = '3.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'


compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	"--limit-modules", "java.base,io.calimero.core",
	"--add-reads", "io.calimero.serial.provider.rxtx=ALL-UNNAMED"
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(19))
    }
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption("-add-reads", "io.calimero.serial.provider.rxtx=ALL-UNNAMED")
}

tasks.withType(Jar).configureEach {
    from("${projectDir}") {
        include 'LICENSE.txt'
        into "META-INF"
    }
    if (name == 'sourcesJar') {
        from("${projectDir}") {
            include 'README.md', 'build.gradle', 'settings.gradle', 'gradle*/**'
        }
    }
}

dependencies {
    api "io.calimero:calimero-core:${version}"
    implementation 'com.neuronrobotics:nrjavaserial:5.2.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero RXTX Provider'
                description = 'Serial communication provider using RXTX'
                url = 'https://github.com/calimero-project/calimero-rxtx'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/calimero-project/calimero-rxtx.git'
                    url = 'https://github.com/calimero-project/calimero-rxtx.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
